import agents.PlanNode;
import PlanIdInfo;

plan DeconstructivePlan extends Plan
{
	#handles event MoveEvent ev;
	#posts event MoveEvent me;
	#posts event PrintEvent pe;
	#posts event UpdateEvent ue;

    #uses interface BWAgent ag;
    
	#modifies data Order order;
	#modifies data Movable mv;
	#modifies data GoalNew gl;
	#reads data Blocks bl;

    public int pid = 1;

	public logical String top;
	public logical String first;
	public logical String second;
	logical int a;
	logical int b;
	public int thisState ;

    public PlanInstanceInfo getInstanceInfo(){
        return ag.planInfo(pid,thisState);
    }

	boolean 
	relevant(MoveEvent m)
	{
		return true;

	}
	
	context()
	{
        order.get(first,second) &&         // find two blocks, one above the other
		!ag.isGoalTower(first.getValue()) && //REMOVABLE
		assign1() &&						// assign the state to 1
        !order.get(top,first.getValue()) &&   // such that the first is free to move
        !second.getValue().equals("table") &&         // find two blocks, one above the other
        ((!order.get(top,first.getValue()) && 		// if the action condition is true 
        !second.getValue().equals("table"))         // then assign the state to 2
		? assign2() : true); 
	}
	body()
	{
		ag.steps++;
		@test(ev.recursive > 0,null);
        ag.writeLog("DeconstructivePlan: Moving block "+first.getValue()+" onto the table");
		order.add(first.getValue(),"table");
		@subtask(ue.update());
		@subtask(pe.print());
		if (ag.goalsRemaining() > 0)
		{
			@subtask(me.move(ev.recursive-1));
		}
	}
	
	boolean assign1()
	{
		thisState = 1;
		return true;
	}
	boolean assign2()
	{
		thisState = 2;
		return true;
	}

	#reasoning method 
	pass()
	{
        ag.record(pid, true, ag.records[ev.recursive], ev.recursive);
		//ag.records[ev.recursive] = "000";
        //ag.writeLog("DeconstructivePlan Passed.");
	}
	
	#reasoning method
	fail()
	{
        ag.record(pid, false, ag.records[ev.recursive], ev.recursive);
        //ag.writeLog("DeconstructivePlan Failed.");
	}

	#reasoning method 
	stateTest()
	{
 		/* Should come here only if the context is true */
		thisState = 1;

		/* If the MOVE precondition also holds then state is 2*/
		if(
        order.get(first,second) &&            // find two blocks, one above the other
        !order.get(top,first.getValue()) &&   // such that the first is free to move
        !second.getValue().equals("table") // prevent moving a block on the table to another place on the table
		) {
			thisState = 2;
		}
		return true;
	}

}

