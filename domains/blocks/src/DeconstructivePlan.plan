import agents.PlanNode;
import PlanIdInfo;

plan DeconstructivePlan extends Plan
{
	#handles event MoveEvent ev;
	#posts event MoveEvent me;
	#posts event PrintEvent pe;
	#posts event UpdateEvent ue;

    #uses interface BWAgent ag;
    
	#modifies data Order order;
	#modifies data Movable mv;
	#modifies data GoalNew gl;
	#reads data Blocks bl;

    public int pid = 1;

	public logical String first;
	public logical String second;
	logical int a;


    public PlanInstanceInfo getInstanceInfo(){
        return ag.planInfo(pid);
    }

	static boolean 
	relevant(MoveEvent m)
	{
		return true;
	}
	
	context()
	{
        order.get(first,second) &&         // find two blocks, one above the other
		!ag.isGoalTower(first.getValue()) &&  //REMOVABLE 
		!second.getValue().equals("table") && // do not move from table to table 
		assign("1") &&
        mv.get(a,first.getValue()) &&  // such that the first is free to move
		assign("2");
	}
	body()
	{
		ag.steps++;
		@test(ev.recursive > 0,null);
        ag.writeLog("DeconstructivePlan: Moving block "+first.getValue()+" onto the table");
		order.add(first.getValue(),"table");
		@subtask(ue.update());
		@subtask(pe.print());
		if (ag.goalsRemaining() > 0)
		{
			@subtask(me.move(ev.recursive-1));
		}
	}

	boolean assign (String value)
	{
		ag.decon = value;
		return true;
	}
	
	#reasoning method 
	pass()
	{
        ag.record(pid, true, ag.records[ev.recursive], ev.recursive);
		//ag.records[ev.recursive] = "000";
        //ag.writeLog("DeconstructivePlan Passed.");
	}
	
	#reasoning method
	fail()
	{
        ag.record(pid, false, ag.records[ev.recursive], ev.recursive);
        //ag.writeLog("DeconstructivePlan Failed.");
	}

}

