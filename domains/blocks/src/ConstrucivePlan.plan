import agents.PlanNode;
import PlanIdInfo;
import aos.jack.jak.beliefset.BeliefSetException;

plan ConstructivePlan extends Plan
{
	#handles event MoveEvent ev;
	#posts event MoveEvent me;
	#posts event PrintEvent pe;
	#posts event UpdateEvent ue;

	#uses interface BWAgent ag;
	
	#modifies data Order order;
	#modifies data Movable mv;
	#modifies data GoalNew gl;
	#reads data Blocks bl;

    public int pid = 0;

	public logical String first; 
	public logical String second;
	logical int a;
	logical int b;	
	static boolean condition;

    public PlanInstanceInfo getInstanceInfo(){
        return ag.planInfo(pid);
    }

	static boolean 
	relevant(MoveEvent m)
	{		
		return true;
	}
	
	context()
	{
		gl.get(first,second) &&   // and where the goal is to have the first on the second
        ag.isGoalTower(second.getValue()) &&     // and where the second is a goal tower
		assign("1") &&
		mv.get(a,first.getValue()) && mv.get(b,second.getValue()) && // both movable
		!first.getValue().equals(second.getValue()) &&  // that are unique
		!first.getValue().equals("table") ;           // and not references to tables
	}
	body()
	{	
		ag.steps++;
		@test(ev.recursive > 0,null);
        ag.writeLog("ConstructivePlan: Moving block "+first.getValue()+" onto block "+second.getValue());
		order.add(first.getValue(),second.getValue());
		@subtask(ue.update());
		@subtask(pe.print());
		if(ag.goalsRemaining() > 0)
		{
			@subtask(me.move(ev.recursive-1));
		}

	}	
	
	boolean assign(String value)
	{
		ag.con = value;
		return true;
	}

	#reasoning method 
	pass()
	{
        ag.record(pid, true, ag.records[ev.recursive], ev.recursive);
		//ag.records[ev.recursive] = "000"; // add to the record that the process is successful 
	}
	
	#reasoning method
	fail()
	{
        ag.record(pid, true, ag.records[ev.recursive], ev.recursive);
        //ag.writeLog("false Failed.");	
	}


}

